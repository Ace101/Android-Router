apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

group = "com.joybar.router"
version = "1.0.1"

def siteUrl = 'https://github.com/myjoybar/Android-Router'
def issuesUrl = 'https://github.com/myjoybar/Android-Router/issues'
def gitUrl = 'https://github.com/myjoybar/Android-Router.git'

install {
    repositories.mavenInstaller.pom.project {
        packaging 'jar'
        name 'Router'
        artifactId "router"
        description 'A simple and easy to use Android Router'
        url siteUrl

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
        }
        developers {
            developer {
                id 'joybar'
                name 'joybar'
                email 'myjoybar@gmail.com'
            }
        }
    }
}

task jar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    options.encoding = 'UTF-8'
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.getDestinationDir()
    classifier = 'javadoc'
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

bintray {
    Properties properties = new Properties()
    boolean hasLocalFile = false
    if (project.rootProject.file('local.properties').exists()) {
        hasLocalFile = true
        properties.load(project.rootProject.file('local.properties').newDataInputStream())
    }
    user = hasLocalFile ? properties.getProperty("bintray.user") : ""
    key = hasLocalFile ? properties.getProperty("bintray.apikey") : ""

    configurations = ['archives']
    pkg {
        repo = "Andorid-Maven"
        name = "router"
        websiteUrl = siteUrl
        issueTrackerUrl = issuesUrl
        vcsUrl = gitUrl
        labels = ["Android", "Router"]
        licenses = ["Apache-2.0"]
        publicDownloadNumbers = true
        publish = true
    }
}

//./gradlew javadocJar
//./gradlew sourcesJar
//./gradlew install
//./gradlew bintrayUpload
